DSIRCNV2 ;EWL - Document Storage System Inc - continuation of conversion routine ;05/21/2010 15:14
 ;;7.2;RELEASE OF INFORMATION - DSSI;**1**;May 21, 2010;Build 8
 ;Copyright 1995-2010, Document Storage Systems, Inc., All Rights Reserved
 ;
 ;DBIA# Supported Reference
 ;----- --------------------------------
 Q
V721 ;EWL - Document Storage System Inc - conversion routine for 7.2.1 patch
 S MSG="V72^DSIRCNV2 FIXADDR started at "_$$FMTE^XLFDT($$NOW^XLFDT,2) D MES^DSICXPDU(MSG,1)
 D FIXADDR
 S MSG="V72^DSIRCNV2 FIXADDR completed at "_$$FMTE^XLFDT($$NOW^XLFDT,2) D MES^DSICXPDU(MSG,1)
 S MSG="V72^DSIRCNV2 NONODE5 started at "_$$FMTE^XLFDT($$NOW^XLFDT,2) D MES^DSICXPDU(MSG,1)
 D NONODE5
 S MSG="V72^DSIRCNV2 NONODE5 completed at "_$$FMTE^XLFDT($$NOW^XLFDT,2) D MES^DSICXPDU(MSG,1)
 S MSG="V72^DSIRCNV2 FIXPHFAX started at "_$$FMTE^XLFDT($$NOW^XLFDT,2) D MES^DSICXPDU(MSG,1)
 D FIXPHFAX
 S MSG="V72^DSIRCNV2 FIXPHFAX completed at "_$$FMTE^XLFDT($$NOW^XLFDT,2) D MES^DSICXPDU(MSG,1)
FIXADDR ;
 N F12,F92,IEN,T1,T2,CT,FDA
 S F12=19620.12,F92=19620.92,IEN=0,CT=0
 F  S IEN=+$O(^DSIR(F92,IEN)) Q:'IEN  D
 .S T1=$P(^DSIR(F92,IEN,0),"^") Q:$L(T1,";")>1
 .S T2=$P(^DSIR(F92,IEN,0),"^",8)
 .I (T1=+T2) D
 ..I +$P(^DSIR(F12,T1,0),"^",3)=0 D
 ...S $P(^DSIR(F92,IEN,0),U)=T2
 Q
FIXPHFAX ;
 N REQADDR,RIEN,AIEN,RF,AF,PHONE,FAX
 S RF=19620.12,AF=19620.92,RIEN=0
 F  S RIEN=$O(^DSIR(RF,RIEN)) Q:'RIEN  D
 .Q:$P(^DSIR(RF,RIEN,0),U,3)'=""
 .S REQADDR=$$GTREQADR(RIEN),AIEN=+$P($G(^DSIR(RF,RIEN,5)),U)
 .Q:AIEN=0
 .Q:$D(^DSIR(AF,AIEN,0))=0
 .;Corrected the next 2 lines
 .K FDA S FDA(AF,AIEN_",",1.01)=$P(REQADDR,U,7)
 .S FDA(AF,AIEN_",",1.02)=$P(REQADDR,U,8)
 .D:$D(FDA) FILE^DIE("","FDA")
 Q
GTREQADR(RPTR) ; GET AN ADDRESS FROM 19620.12
 ; GET ADDRESS INFO RETURNS THE FOLLOWING PIECES
 ; 1 = ADDRESS 1
 ; 2 = ADDRESS 2
 ; 3 = ADDRESS 3
 ; 4 = CITY
 ; 5 = STATE POINTER TO FILE 5
 ; 6 = ZIP
 ; 7 = PHONE
 ; 8 = FAX
 N APTR,RET,RFI
 S APTR=+$G(RPTR),RET="",RFI=19620.12
 I $D(^DSIR(RFI,APTR,2)) D
 .S RET=^DSIR(RFI,APTR,2)
 .; GET FAX NUMBER
 .S $P(RET,U,8)=$S($D(^DSIR(RFI,APTR,4)):^DSIR(RFI,APTR,4),1:"")
 S:'$D(^DSIR(RFI,APTR,0)) RET="-1^ADDRESS NOT FOUND"
 Q RET
NONODE5 ; SCAN REQUESTORS FOR NO NODE 5
 N RIEN,RDFN,RADDR,RADDRNP,RFI,RFDA,AIEN,APTR,ADDRNP,AFI,DONE
 S RFI=19620.12,AFI=19620.92,(RIEN,AIEN,DONE)=0
 F  S RIEN=$O(^DSIR(RFI,RIEN)) Q:'RIEN  D
 .;PROCESS IF NO NODE 5 VALUE
 .I '+$G(^DSIR(RFI,RIEN,5)) D
 ..;GET ADDRESS FROM THE REQUESTOR RECORD
 ..S RADDR=$$GTREQADR(RIEN)
 ..S AIEN=$$PADTOADD(RIEN,RADDR,RIEN)
 ..S RFDA(RFI,RIEN_",",5)=AIEN
 ..D FILE^DIE("","RFDA","EMSG") K RFDA
 Q
PADTOADD(PAT,ADR,RIEN) ; COPY PATIENT ADDRESS TO ADDRESS FILE
 ;INPUT 
 ; PAT - PATIENT OR UNREGISTERED PATIENT POINTER
 ; ADR - 7 OR 8 PIECE ADDRESS STRING (8TH=FAX)
 ; RIEN - REQUESTOR IEN OR POINTER
 N AFI,RDFN,FDA,IEN1,EMSG
 S AFI="19620.92",RDFN=RIEN_";DSIR(19620.12,"
 S FDA(AFI,"+1,",.01)=PAT
 S FDA(AFI,"+1,",.02)=$P(ADR,U,1)
 S FDA(AFI,"+1,",.03)=$P(ADR,U,2)
 S FDA(AFI,"+1,",.11)=$P(ADR,U,3)
 S FDA(AFI,"+1,",.04)=$P(ADR,U,4)
 S FDA(AFI,"+1,",.05)=$P(ADR,U,5)
 S FDA(AFI,"+1,",.06)=$P(ADR,U,6)
 S FDA(AFI,"+1,",1.01)=$P(ADR,U,7)
 S FDA(AFI,"+1,",1.02)=$P(ADR,U,8)
 S FDA(AFI,"+1,",.08)=RDFN
 D UPDATE^DIE("","FDA","IEN1","EMSG")
 Q IEN1(1)
